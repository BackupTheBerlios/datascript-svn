/* BSD License
 *
 * Copyright (c) 2009, Harald Wellmann, Harman/Becker Automotive Systems
 * All rights reserved.
 * 
 * This software is derived from previous work
 * Copyright (c) 2003, Godmar Back.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 * 
 *     * Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 * 
 *     * Redistributions in binary form must reproduce the above
 *       copyright notice, this list of conditions and the following
 *       disclaimer in the documentation and/or other materials provided
 *       with the distribution.
 * 
 *     * Neither the name of Harman/Becker Automotive Systems or
 *       Godmar Back nor the names of their contributors may be used to
 *       endorse or promote products derived from this software without
 *       specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
grammar de.berlios.datascript.DataScript with org.eclipse.xtext.common.Terminals

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate dataScript "http://www.berlios.de/datascript/DataScript"

Model :
    (package = Package)?
	(imports += Import)*
	(elements += Element)*
	;
	
/**
 * Package definition for the current file. Similar to Java.
 */
Package
    : "package" name=PackageName ';'
    ;
    
PackageName
    : ID ('.' ID)*
    ;    	

/**
 * Import of definitions from another resource. Similar to Java.
 * Importing individual types is currently not supported, an import always imports all
 * types from the given resource. The wildcard syntax is forward compatible to a 
 * future implementation of individual imports.
 */	
Import 
    : 'import' importURI=PackagePath '.' '*' ';'
	;
	
/**
 * An 
 */	
PackagePath returns ecore::EString 
    : ID ('.' ID)*
    ;	
	
/**
 * Abstract rule.
 * Every Value has a Type.
 */	
Type 
    : SimpleType
    | ComplexType
    | FunctionType
    | ArrayType
    ;
    
FunctionType
    : result=[Type]
    ;    

ArrayType
    : elementType=[Type] field=[Value]
    ;

/**
 * An Element is a top-level definition of a Model.
 */    
Element 
    : Constant
    | ComplexType
    ;


//ValueBase returns Type
//    : 'bogus' valueType=Type
//    ;

//AbstractValue
//    : 'bogus' valueType=Type 
//    | Value
//    ;
    
/**
 * Abstract rule.
 * Every Expression has a Value of a given Type. We also regard a type name reference
 * as an expression of type "Type".
 * (Analogy: In Java, every object is an instance of type class, and a class literal
 * Foo.class is an instance of class Class<?>.
 */
Value
    : Element
    | EnumMember
    | Field
    | Function
    | Parameter
    | SqlFieldDefinition
//    | '$bogus$' valueType=Type
    ;    
            
        
Constant
    : 'const' type=TypeReference name=ID '=' value=Expression ';'
    ;

/**
 * A ComplexType is a type defined by the user with a given name. It can be referenced
 * in other complex types by its name.
 */    
ComplexType
    : Subtype 
    | EnumType
    | CompoundType
    | SqlDatabaseType
    | SqlTableType
    | SqlIntegerType
    ;

/**
 * A Subtype specializes a given base type and assigns a new name to it.
 * (The specialization is just a rename currently, but it is planned to add things like
 * range constraints for integer types.
 */    
Subtype
    : 'subtype' type=TypeReference name=ID ';'
    ;


EnumType
    : 'enum' type=IntegerType name=ID '{'
       members += EnumMember
       (',' members += EnumMember)*
      '}' ';'
    ;
    
EnumMember
    : name = ID ('=' value = Expression)? 
    ;    

	
SimpleType
	: IntegerType
	| StringType
	;
	
IntegerType
    : builtIn=BuiltInIntegerType
    | BitFieldType
    ;	
	
BuiltInIntegerType
    : "int8"
    | "int16"
    | "int32"
    | "int64"
    | "uint8"
    | "uint16"
    | "uint32"
    | "uint64"
    ;	
	
StringType
    : builtIn="string"
    ;	
	
BitFieldType
    : builtIn=('int' | 'bit')  ( ':' length=IntegerLiteral | '<' length=ShiftExpression '>' )
    ;	

/**
 * Abstract rule.
 */
CompoundType
    : SequenceType
    | ChoiceType
    ;
    
SequenceType
    : (union ?= "union")? 
       name=ID (parameters = ParameterList)?    
      '{' 
      (members += Field)+
      (functions += Function)* 
      '}' ';'
    ;
    
Field
    : (alignment = Alignment)?
      (label = Label)?
      type = TypeReference 
      name = ID 
      (array = Array)? 
      (init = FieldInitializer)? 
      (opt = OptionalClause)? 
      (condition ?= FieldCondition)? ';'     
    ;
    
Alignment
    : 'align' '(' value=IntegerLiteral ')' ':' 
    ;
    
Label
    : expr=Expression ':'
    ;       
    
Array
    : '[' (length=Expression)? ']'
    ;
    
FieldInitializer returns Expression
    : '=' Expression
    ;
    
OptionalClause returns Expression
    : 'if' Expression
    ;
    
FieldCondition returns Expression
    : ':' Expression
    ;                
    
    
ChoiceType
    : 'choice' name=ID (parameters=ParameterList)? 'on' selector=Expression
      '{'
       (members += ChoiceMember)+
       (default = DefaultChoice)?
       (functions += Function)* 
      '}' ';'
    ; 

ChoiceMember
    : ('case' cases+=Expression ':')+ 
      (alternative=ChoiceAlternative)? ';'
    ;    

ChoiceAlternative 
    : type=TypeReference name=ID (array=Array)?
    ;
    
DefaultChoice
    :  'default' ':' (ChoiceAlternative)? ';'
    ;    
    
Function
    : 'function' type=TypeReference name=ID '(' ')' body=FunctionBody 
    ;
    
FunctionBody
    : '{' 'return' expr=Expression ';' '}'
    ;          
    
    
TypeReference
    : SimpleType
    | ref=[ComplexType] (args=ArgumentList)?
    ;
    
    
ParameterList
    : '('
      parameters += Parameter
      (',' parameters += Parameter)*
      ')'
    ; 
    
Parameter
    : type=TypeReference name=ID
    ;    

ArgumentList
    : '(' arguments += TypeArgument (',' arguments += TypeArgument)* ')'
    ;
    
TypeArgument
    : 'explicit' explicit=ID
    |  expr=AssignmentExpression
    ;        

// -- SQL Definitions ----------------------------


SqlDatabaseType
    : 'sql_database' name=ID '{'
      (fields+=SqlTableField)+
      (constraint=SqlConstraint ';')?
      '}' ';'
    ;

SqlTableField
    : table=[SqlTableType] name=ID ';'
    ;

SqlTableType
    : 'sql_table' name=ID (parameters=ParameterList)?
      '{'
      (fields+=SqlFieldDefinition)+
      (constraint=SqlConstraint ';')?
      '}' ';'
    ;

SqlFieldDefinition
    : type=TypeReference 
      name=ID
      (array=Array)?
      (condition=FieldCondition)?
      (constraint=SqlConstraint)?
      ';'
    ;
    
SqlConstraint
    : "sql" constraints += STRING (',' constraints += STRING)* 
    ;    


SqlIntegerType
    : 'sql_integer' name=ID '{'
       (fields += SqlIntegerFieldDefinition)+ 
      '}' ';' 
    ;
    
SqlIntegerFieldDefinition
    : type=IntegerType name=ID (condition=FieldCondition)? ';'
    ;    


       

// -- Expressions -------------------------------------------------

/**
 * Abstract rule
 */
Expression 
    : AssignmentExpression 
      ( {Expression.left = current} 
      operator = "@" 
      op = Operator 
      right = AssignmentExpression
      type=[Type]
      )?
    ;
    
/**
 * Currently not used, causes ambiguity with ParameterList
 */        
CommaExpression returns Expression 
    : AssignmentExpression 
      ( {Expression.left = current} 
      operator = "," 
      right = AssignmentExpression)?
    ;

AssignmentExpression returns Expression 
    : ConditionalExpression 
      ( {Expression.left = current} 
      operator = "=" 
      right = ConditionalExpression)*
    ;
    
ConditionalExpression returns Expression
    : BooleanOrExpression 
      ( {Expression.left = current} 
      operator = "?"
      middle = BooleanOrExpression ':'
      right = BooleanOrExpression )*
    ;
    
BooleanOrExpression returns Expression
    : BooleanAndExpression 
      ({Expression.left = current} 
      operator = "||" 
      right = BooleanAndExpression)* 
    ;
    
BooleanAndExpression returns Expression
    : BitwiseOrExpression 
      ({Expression.left = current} 
      operator = "&&" 
      right = BitwiseOrExpression)*            
    ;
    
BitwiseOrExpression returns Expression
    : BitwiseXorExpression 
      ({Expression.left = current} 
      operator = "|" 
      right = BitwiseXorExpression)*
    ;
    
BitwiseXorExpression returns Expression
    : BitwiseAndExpression        
      ({Expression.left = current} 
      operator = "^" 
      right = BitwiseAndExpression)*
    ;  
      
BitwiseAndExpression returns Expression
    : EqualityExpression        
      ({Expression.left = current} 
      operator = "&" 
      right = EqualityExpression)*
    ;
    
EqualityExpression returns Expression
    : ComparisonExpression      
      ({Expression.left = current} 
      operator = EqualityOperator 
      right = ComparisonExpression)*
    ;
    
EqualityOperator
    : "=="
    | "!="
    ;
    
        
ComparisonExpression returns Expression
    : ShiftExpression      
      ({Expression.left = current} 
      operator = ComparisonOperator 
      right = ShiftExpression)*
    ;
    
ComparisonOperator
    : "<"
    | ">"
    | "<="
    | ">="
    ;
    
ShiftExpression returns Expression
    : AdditiveExpression      
      ({Expression.left = current} 
      operator = ShiftOperator 
      right = AdditiveExpression)*
    ;
      
    
ShiftOperator
    : "<<"
    | ">>"
    ;
    

AdditiveExpression returns Expression 
    : MultiplicativeExpression 
      ( {Expression.left = current} operator=AdditiveOperator 
      right = MultiplicativeExpression)*  
    ;
  
AdditiveOperator 
    : "+" 
    | "-"
    ;
  
MultiplicativeExpression returns Expression 
    : UnaryExpression 
      ( {Expression.left = current} operator=MultiplicativeOperator 
      right = UnaryExpression)*
    ;
  
  
MultiplicativeOperator 
    : "*" 
    | "/"
    | "%"
    ;
    
/**
 * Currently not used. Causes ambiguities with parenthesized PrimaryExpression.
 */    
CastExpression returns Expression
    : ('(' cast=ID ')')? left=UnaryExpression
    ;
    
UnaryExpression returns Expression
    : operator=UnaryOperator left = BuiltInOpExpression
    | BuiltInOpExpression
    ;
    
UnaryOperator
    : '+' 
    | '-'
    | '~'
    | '!'
    ;      

BuiltInOpExpression returns Expression
    : operator=BuiltInOperator left = PostfixExpression
    | PostfixExpression
    ;
    
BuiltInOperator
    : "sizeof"
    | "bitsizeof"
    | "lengthof"
    | "indexof"
    | "sum"
    ;        
    
PostfixExpression returns Expression
    : PrimaryExpression 
    ({Expression.left=current} 
        ( operator='[' right=Expression ']'
        | operator='.'  right=PrimaryExpression
        | '(' operator=')'
        ) 
    )*
    ;
    
    
PrimaryExpression returns Expression
   : ref=[Value]
   | IntegerLiteral
   | StringLiteral
   | operator="(" left = Expression ")"
   ;  
    

IntegerLiteral 
    : value=INT 
    | value=HEX
    | value=BINARY
    ;

StringLiteral 
    : value=STRING     
    ;

  
enum Operator
    : PLUS = '+' 
    | MINUS = '-'
    | MULT = '*' 
    | DIV = '/'
    | MOD = '%'
    ;       

// Override from Xtext: leading caret is not allowed
terminal ID  		
    : ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
    ;

terminal HEX returns ecore::EInt
    : '0' ('x'|'X') ('0'..'9'|'A'..'F'|'a'..'f')+
    ;
    
terminal BINARY returns ecore::EInt
    : ('0'|'1')+ ('b'|'B') 
    ;

/**
 * TODO: Enable doc comments as hidden tokens and access them from the model
 * to generate HTML output Ã  la Javadoc.
 */    
//terminal DOC
//    : "/**" -> "*/"
//    ;
        