package datascript.metamodel;

Model
{
    int32        numTypes;
    Type         types[numTypes];
    
    int32        numFields;
    Field        fields[numFields];
    
    int32        numExpr;
    Expression   expressions[numExpr];
    
    int32        numPackages;
    Package      packages[numPackages];
    
    int32        numNames;
    string       names[numNames];
};

subtype int32 NameId;

Package
{
    int32    packageId;
    NameId   packageName;
    int32    numImports;
    Import   imports[numImports];
    int32    numTypes;
    TypeId   types[numTypes];
};

Import
{
    int8 dummy;
};

subtype int32 TypeId;

Type
{
    TypeId   typeId;
    NameId   typeName;
    Category category;
    int32    size;
    TypeInfo(category) info;
};

enum int8 Category
{
    BUILTIN,
    BITFIELD,
    ENUM,
    SUBTYPE,
    SEQUENCE,
    UNION,
    ARRAY 
};

const int8 useExpression = 0;

// @todo HW: Replace all unions by choices to avoid backtracking.
// @todo HW: Add TypeInstantiationInfo, SubtypeInfo and ChoiceInfo.
// (There is a difference between the definition and an instantiation
// of a parameterized compound type.)

union TypeInfo (Category category)
{
    BuiltInInfo        builtInInfo      : category == Category.BUILTIN;
    BitfieldInfo       bitfieldInfo     : category == Category.BITFIELD;
    EnumInfo           enumInfo         : category == Category.ENUM;
    //SubtypeInfo      subtypeInfo      : category == Category.SUBTYPE;
    SequenceInfo       sequenceInfo     : category == Category.SEQUENCE;
    UnionInfo          unionInfo        : category == Category.UNION;
    ArrayInfo          arrayInfo        : category == Category.ARRAY;
};


enum int8 BuiltInInfo
{
    INT8,
    INT16,
    INT32,
    INT64,
    UINT8,
    UINT16,
    UINT32,
    UINT64,
    STRING
};


subtype int32 FieldId;

// @todo HW: You don't need a TypeRef, simply use the TypeId
subtype int32 TypeRef;

// @todo HW: Use doc comments. Comments must precede commented item.
// @todo HW: Does not make sense. "useExpression" is undefined. The bitfield
// length is always an expression (which may be a constant Atom).
BitfieldInfo
{
    uint32        numBits;   // see DataScript Language Overview 3.2 (The length is not limited)
    ExpressionId  exprId    if useExpression == numBits;
};


EnumInfo
{
    // The bits length of the enum element
    // @todo HW This is not enough, the base type may also be signed or unsigned.
    // Use a TypeRef for the base type (which must be integral).
    int8 bits;

    // Lits of constants
    // @todo HW: rename to "items". "Constants" are different objects in
    // DataScript
    uint32     numConsts;
    
    
    // @todo HW: The idea was to have the metamodel byte-aligned so you can
    // parse it manually, if need be. Thus I would prefer to use the next larger
    // builtin type for the enum item (e.g. replace bit<18> by uint32).
    // To avoid a choice on the base type, you might even use int64 for all
    // enum item values (even if the base type is just bit:2). (This would
    // leave a gap for values >= 2^63, which probably do not occur in practice
    // anyway. 
    bit<bits>  consts[numConsts];
    
    // @todo HW: optionally, add the symbolic name of the enum item
};


CompoundInfo
{
    // Parameters list
    int8       numParams;
    ParamInfo  params[numParams];

    // Fields list
    int16      numFields;
    FieldId    fields[numFields];
};

// @todo HW: Yes, names are not necessary for decoding an instance, but the
// may be helpful for diagnosis and logging. We should provide both options,
// i.e. a model with or without names. Make the nameId optional here by adding
// "if Model.numNames > 0"
ParamInfo
{
    TypeRef type;
    NameId  nameId;     // !!! Probably, it is not necessary to store name of parameters 
};


subtype CompoundInfo SequenceInfo;
subtype CompoundInfo UnionInfo;


ArrayInfo
{
    TypeRef       elementType;  // reference to the type of the array element
    ExpressionId  lengthExpr;   // defines array length calculation expression 
};


Field
{
    int16    pos;
    NameId   name;
    TypeRef  type;
    bit:1    isOptional;
    bit:1    hasConstraint;
    bit:1    hasAlignment;
    bit:1    hasLabel;
    bit:1        hasArguments;

align(8):
    ExpressionId  optional   if 0 != isOptional;
    ExpressionId  constraint if 0 != hasConstraint;
    int32         alignment  if 0 != hasAlignment;
    ExpressionId  label      if 0 != hasLabel;
    
    // @todo HW: An argument list is not an expression, and it should not be
    // treated as such, as it does not have a value or type.
    ExpressionId  arguments  if 0 != hasArguments;      
};


subtype int32 ExpressionId;


//Expression
//{
//    TypeId  type;
//    bit:1   isConstant;
//    bit:7   arity;
//    ExpressionInfo info;
//    Value   value if isConstant;
//};


// @todo HW: It is ok to leave out the precalculated constant value for the 
// moment, but you do need the type. An expression always has a type, which
// may be compound, so have to include the typereference
Expression
{
    Arity arity;
    ExpressionInfo(arity) info;
};


enum uint8 Arity
{
        ATOM,
        UNARY,
        BINARY,
        TERNARY
};

                    
union ExpressionInfo (Arity arity)
{
    Atom    atom    : arity == Arity.ATOM;
    Unary   unary   : arity == Arity.UNARY;
    Binary  binary  : arity == Arity.BINARY;
    Ternary ternary : arity == Arity.TERNARY;
};


Atom
{
        AtomType type;
    AtomInfo(type) info;
};


Unary
{
    UnaryOperator op;
    ExpressionId  arg1;
};


Binary
{
    BinaryOperator op;
    ExpressionId  arg1;
    ExpressionId  arg2;
};


Ternary
{
    TernaryOperator op;
    ExpressionId    arg1;
    ExpressionId    arg2;
    ExpressionId    arg3;
};

// @todo HW: I'm not sure if we need to distinguish local and global references.
enum int8 AtomType
{
    CONST,                              // define explicit and implicit constants (integer literal, enumeration and e.t.) 
    LOCAL_FIELD_REF,    // Reference to the field of the current compound type
    GLOBAL_FIELD_REF,   // Reference to the global field
    PARAM_REF                   // Reference to the parameter
};


union AtomInfo(AtomType type)
{
        Value          value     : type == AtomType.CONST;
        LocalFieldRef  lfieldRef : type == AtomType.LOCAL_FIELD_REF;
        GlobalFieldRef gfieldRef : type == AtomType.GLOBAL_FIELD_REF;
        ParamRef       paramRef  : type == AtomType.PARAM_REF;
        
        // Probably, the definition has not completed yet. 
};


subtype int64 Value;


enum int8 UnaryOperator 
{
        UPLUS,
        UMINUS,
        TILDE,
        BANG,
        LPAREN,
        INDEX,
        LENGTHOF,
        SIZEOF,
        BITSIZEOF,
        SUM,
        FUNCTIONCALL
};

enum int8 BinaryOperator
{
        COMMA,
        LOGICALOR,
        LOGICALAND,
        OR,
        XOR,
        AND,
        EQ,
        NE,
        LT,
        LE,
        GE,
        GT,
        LSHIFT,
        RSHIFT,
        PLUS,
        MINUS,
        MULTIPLY,
        DIVIDE,
        MODULO,
        ARRAYELEM,
        DOT
};
  
enum int8 TernaryOperator
{
        QUESTIONMARK
};


// @todo HW: You could simply use a FieldId here

LocalFieldRef
{
        int16   fieldIndex;             // field index in the current compound type 
};


// @todo HW: The FieldId already is a global id (see Model), and the field
// index within the compound is implicit from the CompoundInfo. Maybe we should
// add a reference from the Field to its containing Compound.

GlobalFieldRef
{
        TypeRef type;                   // Reference to the compound type
        int16   fieldIndex;             // field index in the compound type 
};


// @todo HW: fieldIndex does not make sense here
ParamRef
{
        int8    paramIndex;             // index  in the parameters list of the current compound type
        int16   fieldIndex;             // field index in the compound type 
};

